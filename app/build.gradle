import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp' version '1.8.0-1.0.9'
}

def keystorePropertiesFile = rootProject.file("key.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'com.qtk.kotlintest'
    compileSdk 33

    defaultConfig {
        applicationId "com.qtk.kotlintest"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            ndk {
                abiFilters 'arm64-v8a', 'x86_64'
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (outputFileName.endsWith('.apk')) {
                def date = new SimpleDateFormat("yyyy-MM-dd").format(new Date())
                if (variant.productFlavors[0]?.name == null) {
                    outputFileName = "KotlinTest_${defaultConfig.versionName}_${variant.buildType.name}_${date}.apk"
                } else {
                    outputFileName = "KotlinTest_${defaultConfig.versionName}_${variant.buildType.name}_${variant.productFlavors[0]?.name}_${date}.apk"
                }
            }
        }
    }
}

/*allprojects {
    configurations.all {
        resolutionStrategy {
            force "androidx.activity:activity:1.5.0"
        }
    }
}*/

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'io.github.qtkun:flowbus:1.0.3'
//    implementation project(path: ':flutter')
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'
    implementation 'org.ligboy.retrofit2:converter-fastjson:2.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation project(path: ':LeonidsLib')
    implementation files('libs/jsch-0.1.55.jar')
    //room
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-testing:$room_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation "org.jetbrains.anko:anko-common:$anko_version"

    implementation "org.jetbrains.anko:anko-sqlite:$anko_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    //retrofit2
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    //camerax
    implementation "androidx.camera:camera-camera2:$camera2_version"
    implementation "androidx.camera:camera-view:$camera2_version"
    implementation "androidx.camera:camera-extensions:$camera2_version"
    implementation "androidx.camera:camera-lifecycle:$camera2_version"
    //hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    //moshi
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.12.0'
    ksp 'com.github.bumptech.glide:ksp:4.14.2'
    implementation ("com.github.bumptech.glide:recyclerview-integration:4.12.0") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    //Coil
    implementation 'io.coil-kt:coil:1.1.1'
    //dataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    //workManager
    implementation "androidx.work:work-runtime:$work_manager_version"
    implementation "androidx.work:work-runtime-ktx:$work_manager_version"
    //高德
    implementation 'com.amap.api:3dmap:latest.integration'
//    implementation 'com.amap.api:search:latest.integration'
//    implementation 'com.amap.api:location:latest.integration'

    implementation 'com.gitee.zackratos:UltimateBarX:0.8.0'
    //flexbox
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation 'com.github.ome450901:SimpleRatingBar:1.5.1'

    implementation "com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-java:$gsy_version"
    implementation "com.github.CarGuo.GSYVideoPlayer:GSYVideoPlayer-exo2:$gsy_version"
    implementation "com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-arm64:$gsy_version"
    implementation "com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-x64:$gsy_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'com.github.Tencent.soter:soter-wrapper:2.0.7'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'com.jawnnypoo:physicslayout:3.0.1'
    implementation 'com.blankj:utilcodex:1.31.1'
}

kapt {
    correctErrorTypes true
}

/**
 * 关闭不需要执行的task，提高build速度
 */
this.tasks.whenTaskAdded {
    task ->
        if (task.name.contains('UnitTest') || task.name.contains("AndroidTest")) {
            task.enabled = false
        }
}
/*
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.9"
    }
}*/
